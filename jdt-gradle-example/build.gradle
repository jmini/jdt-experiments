buildscript {
    dependencies {
        classpath 'com.diffplug.gradle:goomph:3.31.0'
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.10.2' apply false
}

wrapper {
    gradleVersion = '7.1'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'com.diffplug.eclipse.mavencentral'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    repositories {
        mavenCentral()
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier = 'javadoc'
    }

    test {
        useJUnitPlatform()
    }

    spotless {
      java {
        importOrder 'java', 'javax', 'org', 'com', ''
        removeUnusedImports()

        // available versions are: https://github.com/diffplug/spotless/tree/master/lib-extra/src/main/resources/com/diffplug/spotless/extra/eclipse_jdt_formatter
        eclipse('4.13.0').configFile('.settings/org.eclipse.jdt.core.prefs')
      }
    }

}
